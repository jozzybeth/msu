@echo off
REM  ќтключили отображение на экран выполн€емых команд этого файла
REM  =========================================================================
REM  | Ёто коммандный файл m.bat с доопределением переменной окружени€ path  |
REM  =========================================================================
REM ѕроверка наличи€ параметра при вызове коммандного файла m.bat :
REM %1   - первый (и единственный) параметр, 
REM %~n1 - им€ файла, переданного как параметр, без расширени€
if .%1.==.. goto USAGE
if exist %1 goto start

:USAGE
REM сообщение о том, что при запуске надо указывать
REM  им€ компилируемого файла с расширением  
echo usage: m program.asm
  goto TheEnd
 
:start 
REM указание мест, где следует искать дистрибутив MASM:
  set path= %path%;c:\masm 6.14\bin;c:\masm32\bin;..\bin;..\..\bin
REM  c:\masm 6.14 - на факультете,  c:\masm32 - дома 
  
REM ѕараметры (компил€ции) ассемблера (ml.exe -?):
REM    /Fl     создание листинга (%~n1.lst)
REM    /I...   каталог с включаемыми (по include) файлами
REM    /c      трансл€ци€ в объектный файл (%~n1.obj), без компоновки
REM    /coff   формат объектного файла Ч COFF (Common Object File Format)
REM    /nologo запрет выдавать на экран версию компил€тора и др.
REM -------------------------------------------------------------------------------
ml.exe /Fl /Ic:\masm32\include /I"c:\tools\masm 6.14\include" /c /coff /nologo %1
REM -------------------------------------------------------------------------------
REM ѕроверка успешности компил€ции
if errorlevel 1 goto errasm

REM ѕараметры компоновки (link.exe -?):
REM    /libpath:...       каталог с вспомогательными библиотеками
REM    /subsystem:console указание строить консольное Windows-приложение.
REM    /nologo            запрет выдавать на экран версию редактора св€зей и др.
REM --------------------------------------------------------------------------------------------
link.exe /libpath:"c:\masm 6.14\lib" /libpath:c:\masm32\lib /subsystem:console /nologo %~n1.obj 
REM --------------------------------------------------------------------------------------------
REM  ”даление промежуточного объектного файла %~n1.obj
del %~n1.obj 
  goto TheEnd

:errasm
REM —ообщение о том, что в процессе компил€ции были зафиксированы ошибки
  echo Assembler Error !!!!!!!!!!!!
  goto TheEnd

:TheEnd
rem pause
rem %~n1.exe
rem echo on
